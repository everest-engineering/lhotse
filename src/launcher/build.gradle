buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'org.springframework.boot'
}

apply plugin: 'jacoco'

springBoot {
    buildInfo()
}

bootBuildInfo.mustRunAfter processResources

bootBuildImage {
    imageName = "$organisation/$rootProject.name:$project.version"
}

compileJava.dependsOn(processResources)

test {
    systemProperty "org.gradle.project.buildDir", "$project.buildDir"
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

dependencies {
    file("../").eachDir { p ->
        if (p.name != "launcher") {
            implementation project(":${p.name}")
        }
    }

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    implementation "engineering.everest.axon:crypto-shredding-extension:${axonCryptoShreddingVersion}"
    implementation "engineering.everest.axon:command-distribution-extension:${axonHazelcastCommandDistributionVersion}"
    implementation "engineering.everest.starterkit:storage:${storageVersion}"
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.keycloak:keycloak-spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation "com.h2database:h2:${h2Version}"
    implementation "org.axonframework:axon-micrometer:${axonVersion}"
    implementation "io.micrometer:micrometer-core:${microMeterPrometheusVersion}"
    implementation "io.micrometer:micrometer-registry-prometheus:${microMeterPrometheusVersion}"
    implementation "io.prometheus:simpleclient:${prometheusSimpleClientVersion}"
    implementation "io.prometheus:simpleclient_hotspot:${prometheusSimpleClientVersion}"
    implementation "io.prometheus:simpleclient_pushgateway:${prometheusSimpleClientVersion}"
    implementation "org.liquibase:liquibase-core:${liquibaseVersion}"
    implementation "org.postgresql:postgresql:${postgresDriverVersion}"
    implementation "com.amazonaws:aws-java-sdk-s3:${awsSdkVersion}"

    constraints {
        implementation("org.bouncycastle:bcprov-jdk15on:${bouncyCastleVersion}") {
            because 'Force transitive upgrade to avoid CVE'
        }
        implementation("org.bouncycastle:bcpkix-jdk15on:${bouncyCastleVersion}") {
            because 'Force transitive upgrade to avoid CVE'
        }
        implementation("org.bouncycastle:bcprov-ext-jdk15on:${bouncyCastleVersion}") {
            because 'Force transitive upgrade to avoid CVE'
        }
    }

    testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.boot:spring-boot-starter-webflux"
    testImplementation "org.apache.commons:commons-text:${commonsTextVersion}"
    testImplementation "org.bouncycastle:bcpkix-jdk15on:${bouncyCastleVersion}"
    testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo'
}

dependencyManagement {
	imports {
		mavenBom "org.keycloak.bom:keycloak-adapter-bom:${keycloakVersion}"
	}
}
