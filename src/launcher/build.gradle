buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'org.springframework.boot'
    id "com.avast.gradle.docker-compose" version "${gradleDockerComposeVersion}"
}

apply plugin: 'jacoco'

springBoot {
    buildInfo()
}

bootRun {
    args = []
    jvmArgs = [
            "--add-opens", "java.base/java.lang=ALL-UNNAMED",
            "--add-opens", "java.base/java.io=ALL-UNNAMED",
            "--add-opens", "java.base/java.util=ALL-UNNAMED",
            "--add-opens", "java.base/java.util.concurrent=ALL-UNNAMED"
    ]
}

bootBuildInfo.mustRunAfter processResources

bootBuildImage {
    imageName = "$organisation/$rootProject.name:$project.version"
}

compileJava.dependsOn(processResources)

test {
    systemProperty "org.gradle.project.buildDir", "$project.buildDir"
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    jvmArgs = [
            "--add-opens", "java.base/java.lang=ALL-UNNAMED",
            "--add-opens", "java.base/java.io=ALL-UNNAMED",
            "--add-opens", "java.base/java.util=ALL-UNNAMED",
            "--add-opens", "java.base/java.util.concurrent=ALL-UNNAMED"
    ]
}

dockerCompose {
    useComposeFiles.add("${rootProject.projectDir}/docker-compose.yml")
    isRequiredBy(tasks.test)
}

dependencies {
    file("../").eachDir { p ->
        if (p.name != "launcher") {
            implementation project(":${p.name}")
        }
    }

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    implementation "engineering.everest.axon:crypto-shredding-extension:${axonCryptoShreddingVersion}"
    implementation "engineering.everest.starterkit:media:${mediaVersion}"
    implementation "engineering.everest.starterkit:storage:${storageVersion}"
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation "io.prometheus:simpleclient:${prometheusSimpleClientVersion}"
    implementation "io.prometheus:simpleclient_hotspot:${prometheusSimpleClientVersion}"
    implementation "io.prometheus:simpleclient_pushgateway:${prometheusSimpleClientVersion}"
    implementation "org.axonframework:axon-micrometer:${axonVersion}"
    implementation "org.axonframework:axon-micrometer:${axonVersion}"
    implementation "org.liquibase:liquibase-core:${liquibaseVersion}"
    implementation "org.postgresql:postgresql:${postgresDriverVersion}"
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'jakarta.persistence:jakarta.persistence-api'

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.boot:spring-boot-starter-webflux"
    testImplementation "org.springframework.security:spring-security-test"
    testImplementation "io.zonky.test:embedded-database-spring-test:${zonkyEmbeddedDbVersion}"
    testImplementation "org.apache.commons:commons-text:${commonsTextVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
    testImplementation "org.keycloak:keycloak-admin-client:${keycloakVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
}
