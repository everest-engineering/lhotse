import com.github.jk1.license.render.*

plugins {
    id 'com.palantir.git-version' version '0.12.3'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'org.springframework.boot' version '2.3.3.RELEASE' apply false
    id 'io.freefair.lombok' version '5.1.1'
    id 'org.owasp.dependencycheck' version '5.3.2.1'
    id 'com.github.jk1.dependency-license-report' version '1.12'
    id 'org.sonarqube' version '2.7'
}

version = gitVersion()

allprojects {
    apply plugin: 'idea'

    ext {
       organisation = 'com.myorganisation'
    }
}

dependencyCheck {
   formats = ['ALL']
}

licenseReport {
    renderers = [new InventoryHtmlReportRenderer()]
}

subprojects {
    group = 'everest.engineering.starter.kit'

    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-library'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'
    apply plugin: 'io.freefair.lombok'

    sourceCompatibility = 1.11
    targetCompatibility = 1.11
    version = gitVersion()

    repositories {
        mavenLocal()
        jcenter()
        maven {
            url  "https://dl.bintray.com/everestengineering/starterkit"
        }
    }

    ext {
        axonCryptoShreddingVersion = '0.9.2'
        axonHazelcastCommandDistributionVersion = '0.9.0'
        mediaVersion = '0.9.1'
        securityVersion = '0.0.15'
        storageVersion = '0.9.1'

        aspectjVersion = '1.9.6'
        awsSdkVersion = '1.11.887'
        axonVersion = '4.4.3'
        bouncyCastleVersion = '1.66'
        commonsLangVersion = '3.11'
        commonsTextVersion = '1.9'
        commonsValidatorVersion = '1.7'
        hazelcastKubernetesVersion = '1.5.4'
        hazelcastVersion = '3.12.10'
        javaXCacheVersion = '1.1.1'
        javaXPersistenceVersion = '2.2'
        liquibaseVersion = '4.1.1'
        microMeterPrometheusVersion = '1.5.5'
        postgresDriverVersion = '42.2.18'
        prometheusSimpleClientVersion = '0.9.0'
        swaggerVersion = '3.0.0'
        springBootVersion = '2.3.4.RELEASE'

        h2Version = '1.4.200'
        hamcrestVersion = '2.2'
        junitVersion = '5.7.0'
        mockitoVersion = '3.5.15'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
        }
    }

    lombok {
        version = "1.18.12"
        generateLombokConfig.enabled = false
    }

    checkstyle {
        toolVersion = '8.32'
        maxWarnings = 0
        configFile = file("${project.rootDir}/build-config/checkstyle/checkstyle.xml")
        sourceSets = [sourceSets.main]
    }

    pmd {
        toolVersion = '6.23.0'
        incrementalAnalysis = true
        ruleSets = []
        ruleSetFiles = files("${project.rootDir}/build-config/pmd/pmd-ruleset.xml")
        sourceSets = [sourceSets.main]
    }

    test {
        useJUnitPlatform()
        test {
            testLogging {
                events "skipped", "failed"
                exceptionFormat "full"
                showStandardStreams = false
            }
        }
    }

    tasks.withType(JavaCompile) {
        options.deprecation = true
    }
}
