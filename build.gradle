import com.github.jk1.license.render.*

plugins {
    id 'com.palantir.git-version' version '0.12.3'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'org.springframework.boot' version '2.2.7.RELEASE' apply false
    id 'io.freefair.lombok' version '5.0.1'
    id 'org.owasp.dependencycheck' version '5.3.2.1'
    id 'com.github.jk1.dependency-license-report' version '1.12'
}

version = gitVersion()

allprojects {
    apply plugin: 'idea'
}

licenseReport {
    renderers = [new InventoryHtmlReportRenderer()]
}

subprojects {
    group = 'everest.engineering.starter.kit'

    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-library'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'
    apply plugin: 'io.freefair.lombok'

    sourceCompatibility = 1.11
    targetCompatibility = 1.11
    version = gitVersion()

    repositories {
        mavenLocal()
        jcenter()
        maven {
            url  "https://dl.bintray.com/everestengineering/starterkit"
        }
    }

    ext {
        axonHazelcastCommandDistributionVersion = '0.0.15'
        mediaVersion = '0.0.13'
        securityVersion = '0.0.10'
        storageVersion = '0.0.13'

        aspectjVersion = '1.9.5'
        awsSdkVersion = '1.11.740'
        axonVersion = '4.3.3'
        bouncyCastleVersion = '1.65'
        commonsLangVersion = '3.10'
        commonsTextVersion = '1.8'
        commonsValidatorVersion = '1.6'
        ehCacheVersion = '3.8.1'
        guavaVersion = '29.0-jre'
        hazelcastKubernetesVersion = '1.5.3'
        hazelcastVersion = '3.12.7'
        javaXCacheVersion = '1.1.1'
        javaXPersistenceVersion = '2.2'
        jjwtVersion = '0.11.1'
        jodaTimeVersion = '2.10.6'
        liquibaseVersion = '3.8.9'
        microMeterPrometheusVersion = '1.3.8'
        postgresDriverVersion = '42.2.8'
        prometheusSimpleClientVersion = '0.7.0'
        swaggerVersion = '2.9.2'
        thumbnailatorVersion = '0.4.11'
        springBootVersion = '2.2.7.RELEASE'

        h2Version = '1.4.200'
        hamcrestVersion = '2.2'
        junitVersion = '5.6.2'
        mockitoVersion = '3.3.3'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
        }
    }

    lombok {
        version = "1.18.12"
        generateLombokConfig.enabled = false
    }

    checkstyle {
        toolVersion = '8.32'
        maxWarnings = 0
        configFile = file("${project.rootDir}/build-config/checkstyle/checkstyle.xml")
        sourceSets = [sourceSets.main]
    }

    pmd {
        toolVersion = '6.23.0'
        incrementalAnalysis = true
        ruleSets = []
        ruleSetFiles = files("${project.rootDir}/build-config/pmd/pmd-ruleset.xml")
        sourceSets = [sourceSets.main]
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "skipped", "failed"
        }
    }

    tasks.withType(JavaCompile) {
        options.deprecation = true
    }
}
