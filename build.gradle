import com.github.jk1.license.render.*

plugins {
    id 'com.palantir.git-version' version '0.12.3'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'org.springframework.boot' version '2.4.0' apply false
    id 'io.freefair.lombok' version '5.1.1'
    id 'org.owasp.dependencycheck' version '6.0.3'
    id 'com.github.jk1.dependency-license-report' version '1.14'
    id 'org.sonarqube' version '3.1.1'
    id 'jacoco'
}

version = gitVersion()

allprojects {
    apply plugin: 'idea'

    ext {
        organisation = 'com.myorganisation'
    }
}

repositories {
    jcenter()
}

subprojects {
    group = 'everest.engineering.starter.kit'

    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-library'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'
    apply plugin: 'io.freefair.lombok'

    sourceCompatibility = 1.11
    targetCompatibility = 1.11
    version = gitVersion()

    repositories {
        mavenLocal()
        jcenter()
        maven {
            url "https://dl.bintray.com/everestengineering/starterkit"
        }
    }

    ext {
        axonCryptoShreddingVersion = '0.9.4'
        axonHazelcastCommandDistributionVersion = '0.9.3'
        mediaVersion = '0.9.2'
        securityVersion = '0.9.2'
        storageVersion = '0.9.2'

        aspectjVersion = '1.9.6'
        awsSdkVersion = '1.11.887'
        axonVersion = '4.4.5'
        bouncyCastleVersion = '1.68'
        commonsLangVersion = '3.11'
        commonsTextVersion = '1.9'
        commonsValidatorVersion = '1.7'
        hazelcastKubernetesVersion = '2.0.2'
        hazelcastVersion = '4.0.3'
        javaXCacheVersion = '1.1.1'
        javaXPersistenceVersion = '2.2'
        liquibaseVersion = '4.2.2'
        microMeterPrometheusVersion = '1.6.2'
        postgresDriverVersion = '42.2.18'
        prometheusSimpleClientVersion = '0.9.0'
        swaggerVersion = '3.0.0'
        springBootVersion = '2.4.0'

        h2Version = '1.4.200'
        hamcrestVersion = '2.2'
        junitVersion = '5.7.0'
        mockitoVersion = '3.5.15'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
        }
    }

    lombok {
        version = "1.18.16"
        generateLombokConfig.enabled = false
        config['lombok.addLombokGeneratedAnnotation'] = 'true'
    }

    checkstyle {
        toolVersion = '8.39'
        maxWarnings = 0
        configFile = file("${project.rootDir}/build-config/checkstyle/checkstyle.xml")
        sourceSets = [sourceSets.main]
    }

    pmd {
        toolVersion = '6.23.0'
        incrementalAnalysis = true
        ruleSets = []
        ruleSetFiles = files("${project.rootDir}/build-config/pmd/pmd-ruleset.xml")
        sourceSets = [sourceSets.main]
    }

    test {
        useJUnitPlatform()
        test {
            testLogging {
                events "skipped", "failed"
                exceptionFormat "full"
                showStandardStreams = false
            }
        }
    }

    tasks.withType(JavaCompile) {
        options.deprecation = true
    }
}

dependencyCheck {
    formats = ['ALL']
    scanConfigurations = ['bootArchives', 'productionRuntimeClasspath']
}

licenseReport {
    renderers = [new InventoryHtmlReportRenderer()]
}

tasks.register("codeCoverageReport", JacocoReport) {
    subprojects { subproject ->
        subproject.plugins.withType(JacocoPlugin).configureEach {
            subproject.tasks.matching({ t -> t.extensions.findByType(JacocoTaskExtension) }).configureEach { testTask ->
                sourceSets subproject.sourceSets.main
                executionData(testTask)
            }

            subproject.tasks.matching({ t -> t.extensions.findByType(JacocoTaskExtension) }).forEach {
                rootProject.tasks.codeCoverageReport.dependsOn(it)
                it.finalizedBy(jacocoTestReport)
            }
        }
    }

    reports {
        xml.enabled true
        html.enabled true
    }
}

sonarqube {
    properties {
        property 'sonar.projectName', 'Lhotse'
        property 'sonar.projectKey', 'everest-engineering_lhotse'
        property 'sonar.organization', 'everestengineering'
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.coverage.jacoco.xmlReportPaths', ["${project.rootDir}/build/reports/jacoco/codeCoverageReport/codeCoverageReport.xml"]
        property 'sonar.test.exclusions', '**/*Test.java'
    }
}
