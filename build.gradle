import com.github.jk1.license.render.*

plugins {
    id 'com.palantir.git-version' version '0.12.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.springframework.boot' version '2.4.8' apply false
    id 'org.owasp.dependencycheck' version '6.2.2'
    id 'com.github.jk1.dependency-license-report' version '1.16'
    id 'org.sonarqube' version '3.3'
    id 'com.diffplug.spotless' version '6.0.0'
    id 'jacoco'
}

version = gitVersion()

allprojects {
    apply plugin: 'idea'

    ext {
        organisation = 'com.myorganisation'
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

subprojects {
    group = 'everest.engineering.starter.kit'

    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-library'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'
    apply plugin: 'com.diffplug.spotless'

    version = gitVersion()

    repositories {
        mavenCentral()
        mavenLocal()
    }

    ext {
        axonCryptoShreddingVersion = '0.9.7'
        axonHazelcastCommandDistributionVersion = '0.9.6'
        mediaVersion = '0.9.6'
        storageVersion = '0.9.10'

        awsSdkVersion = '1.11.887'
        axonVersion = '4.5.5'
        bouncyCastleVersion = '1.69'
        commonsLangVersion = '3.12.0'
        commonsTextVersion = '1.9'
        commonsValidatorVersion = '1.7'
        ehCacheVersion = '3.9.7'
        hazelcastKubernetesVersion = '2.2.3'
        hazelcastVersion = '4.2.2'
        javaXCacheVersion = '1.1.1'
        javaXPersistenceVersion = '2.2'
        jsonHelperVersion = '20210307'
        keycloakVersion = '15.0.2'
        liquibaseVersion = '4.6.1'
        lombokVersion = '1.18.22'
        microMeterPrometheusVersion = '1.8.0'
        postgresDriverVersion = '42.3.1'
        prometheusSimpleClientVersion = '0.11.0'
        springBootVersion = '2.6.0'
        swaggerVersion = '3.0.0'
        webfluxVersion = '2.5.5'

        h2Version = '1.4.200'
        hamcrestVersion = '2.2'
        junitVersion = '5.8.1'
        mockitoVersion = '4.0.0'
        slf4jTestVersion = '2.4.0'
        undercouchVersion = '4.1.2'
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(11)
        }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
        }
    }

    dependencies {
        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

        testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
        testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    }

    checkstyle {
        toolVersion = '8.44'
        maxWarnings = 0
        configFile = file("${project.rootDir}/build-config/checkstyle/checkstyle.xml")
        sourceSets = [sourceSets.main]
    }

    pmd {
        toolVersion = '6.36.0'
        incrementalAnalysis = true
        ruleSets = []
        ruleSetFiles = files("${project.rootDir}/build-config/pmd/pmd-ruleset.xml")
        sourceSets = [sourceSets.main]
    }

    spotless {
        format 'misc', {
            target '*.gradle', '*.md', '.gitignore'
            trimTrailingWhitespace()
            indentWithSpaces(4)
            endWithNewline()
        }
        java {
            eclipse().configFile("$rootDir/build-config/eclipse-formatter-config.xml")
            indentWithSpaces(4)
            trimTrailingWhitespace()
            removeUnusedImports()
        }
    }

    test {
        useJUnitPlatform()
        test {
            testLogging {
                events "skipped", "failed"
                exceptionFormat "full"
                showStandardStreams = false
            }
        }
    }

    tasks.withType(JavaCompile) {
        options.deprecation = true
    }
}

dependencyCheck {
    formats = ['ALL']
    scanConfigurations = ['bootArchives', 'productionRuntimeClasspath']
}

licenseReport {
    renderers = [new InventoryHtmlReportRenderer()]
}

apply from: 'sonar.gradle'
