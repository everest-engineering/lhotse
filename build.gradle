import com.github.jk1.license.render.*

plugins {
    id 'com.palantir.git-version' version '0.12.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.springframework.boot' version '2.4.8' apply false
    id 'org.owasp.dependencycheck' version '6.2.2'
    id 'com.github.jk1.dependency-license-report' version '1.16'
    id 'org.sonarqube' version '3.3'
    id 'jacoco'
}

version = gitVersion()

allprojects {
    apply plugin: 'idea'

    ext {
        organisation = 'com.myorganisation'
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

subprojects {
    group = 'everest.engineering.starter.kit'

    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-library'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'

    sourceCompatibility = 1.11
    targetCompatibility = 1.11
    version = gitVersion()

    repositories {
        mavenCentral()
        mavenLocal()
    }

    ext {
        axonCryptoShreddingVersion = '0.9.6'
        axonHazelcastCommandDistributionVersion = '0.9.5'
        mediaVersion = '0.9.4'
        storageVersion = '0.9.7'

        awsSdkVersion = '1.11.887'
        axonVersion = '4.5.1'
        bouncyCastleVersion = '1.69'
        commonsLangVersion = '3.12.0'
        commonsTextVersion = '1.9'
        commonsValidatorVersion = '1.7'
        hazelcastKubernetesVersion = '2.2.3'
        hazelcastVersion = '4.2.1'
        javaXCacheVersion = '1.1.1'
        javaXPersistenceVersion = '2.2'
        jsonHelperVersion = '20210307'
        keycloakVersion = '15.0.2'
        keycloakRestClientVersion = '15.0.2'
        liquibaseVersion = '4.4.0'
        lombokVersion = '1.18.20'
        microMeterPrometheusVersion = '1.7.1'
        postgresDriverVersion = '42.2.18'
        prometheusSimpleClientVersion = '0.11.0'
        springBootVersion = '2.4.8'
        swaggerVersion = '3.0.0'
        webfluxVersion = '2.5.5'

        h2Version = '1.4.200'
        hamcrestVersion = '2.2'
        junitVersion = '5.7.2'
        mockitoVersion = '3.11.2'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
        }
    }

    dependencies {
        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

        testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
        testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    }

    checkstyle {
        toolVersion = '8.44'
        maxWarnings = 0
        configFile = file("${project.rootDir}/build-config/checkstyle/checkstyle.xml")
        sourceSets = [sourceSets.main]
    }

    pmd {
        toolVersion = '6.36.0'
        incrementalAnalysis = true
        ruleSets = []
        ruleSetFiles = files("${project.rootDir}/build-config/pmd/pmd-ruleset.xml")
        sourceSets = [sourceSets.main]
    }

    test {
        useJUnitPlatform()
        test {
            testLogging {
                events "skipped", "failed"
                exceptionFormat "full"
                showStandardStreams = false
            }
        }
    }

    tasks.withType(JavaCompile) {
        options.deprecation = true
    }
}

dependencyCheck {
    formats = ['ALL']
    scanConfigurations = ['bootArchives', 'productionRuntimeClasspath']
}

licenseReport {
    renderers = [new InventoryHtmlReportRenderer()]
}

apply from: 'sonar.gradle'
